#define FUELTYPE_GAS /datum/reagent/fuel
//#define FUELTYPE_ELECTRIC 2

#define COOLDOWN_ENGINE_START "engine_start"

/obj/item/mecha_parts/exosuit_engine
	name = "exosuit fuel engine"
	desc = "A small engine, running on fuel. Has a built-in fuel container."

	max_integrity = 100

	var/fuel_type = FUELTYPE_GAS
	var/fuel_max = 1000
	var/fuel_amount = 0
	var/fuel_consumption = 0.5
	var/comes_prefilled = TRUE

	var/is_functional = TRUE
	var/is_running = FALSE

	var/ignition_power_consumption = 20
	var/ignition_cycle_attempts = 2

	var/datum/looping_sound/engine_running_sound = /datum/looping_sound/exosuit_engine_fuel/sound_loop
	var/datum/looping_sound/engine_running_sound_high = /datum/looping_sound/exosuit_engine_fuel_high/sound_loop
	var/engine_starting_sound = 'sound/machines/generator/generator_start.ogg'
	var/engine_stop_sound = 'sound/machines/generator/generator_end.ogg'

	var/sound_increase = 1

//	var/obj/vehicle/sealed/mecha/ntf/ntf_chassis
	var/obj/item/mecha_parts/mecha_pieces/mecha_body/body

	// Amount of power the engine creates per tick
	var/engine_power_generated = 200 // Change this to revs?
	// Amount of power the engine 'has to use', abstracted as a power cell
	var/obj/item/cell/engine_power/engine_power_pool

	var/obj/item/cell/starting_battery/starter_battery
	var/engine_initial_start_chance = 30

	// For electric engine
	var/is_electric = FALSE

	var/revs = REVS_OFF
	var/revs_selection = REVS_LOW

/obj/item/cell/engine_power
	name = "energy pool"
	desc = "A concept of the max amount of power/motivation an exosuit engine can produce. Should zero when the engines turns off."
	maxcharge = 200
	charge = 0
	starts_full = FALSE

/obj/item/cell/starting_battery
	name = "electric start battery"
	desc = "A small battery for starting a small engine"
	maxcharge = 500
	charge = 500

/obj/item/cell/starting_battery/electric
	name = "large storage battery"
	desc = "A large storage battery, for providing energy to electric motors of a vehicle."
	maxcharge = 20000
	charge_amount = 10
	charge = 20000

/obj/item/mecha_parts/exosuit_engine/Destroy()
	STOP_PROCESSING(SSobj, src)
	engine_stop()
	return ..()

/obj/item/mecha_parts/exosuit_engine/proc/is_active()
	return is_running

/obj/item/mecha_parts/exosuit_engine/proc/add_battery(obj/item/cell/starting_battery/add_battery)
	QDEL_NULL(starter_battery)
	if(add_battery)
		add_battery.forceMove(src)
		starter_battery = add_battery
		return
	starter_battery = new /obj/item/cell/starting_battery (src)

/obj/item/mecha_parts/exosuit_engine/proc/add_power(obj/item/cell/engine_power/add_power)
	QDEL_NULL(engine_power_pool)
	if(add_power)
		add_power.forceMove(src)
		engine_power_pool = add_power
		return
	engine_power_pool = new /obj/item/cell/engine_power (src)

/obj/item/mecha_parts/exosuit_engine/Initialize(mapload)
	.=..()
	if(engine_running_sound)
		engine_running_sound = new engine_running_sound(list(src))
	if(engine_running_sound_high)
		engine_running_sound_high = new engine_running_sound_high(list(src))
	if(comes_prefilled)
		create_reagents(fuel_max, AMOUNT_VISIBLE, list(/datum/reagent/fuel = fuel_max))
	add_battery()
	add_power()

/obj/item/mecha_parts/exosuit_engine/get_fueltype()
	return fuel_type

/obj/item/mecha_parts/exosuit_engine/process()
	if(is_functional && is_running)
		var/current_consumption = fuel_consumption * (revs / 7500)
		if(fuel_amount >= current_consumption)
			fuel_amount = max(0, fuel_amount - current_consumption)

		if(engine_power_pool && !engine_power_pool.is_fully_charged())
			engine_power_pool.give(revs * GLOB.CELLRATE)

		if(starter_battery && !starter_battery.is_fully_charged())
			var/starter_charge_amount = revs * 0.25
			if(engine_power_pool && engine_power_pool.use(starter_charge_amount))
				starter_battery.give(starter_charge_amount * GLOB.CELLRATE)

		if(fuel_amount < current_consumption)
			engine_stop()

/obj/item/mecha_parts/exosuit_engine/obj_break()
	engine_stop()
	is_functional = FALSE
	return ..()

/obj/item/mecha_parts/exosuit_engine/proc/attempt_engine_start()
	if(is_running)
		visible_message(span_warning("[src] is already running"))
		return FALSE
	if(TIMER_COOLDOWN_RUNNING(src, COOLDOWN_ENGINE_START))
		visible_message(span_warning("[src] is already trying to start"))
		return

	S_TIMER_COOLDOWN_START(src, COOLDOWN_ENGINE_START, 2 SECONDS)

	var/can_start = is_functional && fuel_amount > 0
	var/current_start_chance = can_start ? engine_initial_start_chance : 0

	for(var/i in 1 to ignition_cycle_attempts)

		if(!starter_battery)
			playsound(loc, 'sound/effects/refill.ogg', 25, 1, 3) // dead sound
			return FALSE

		if(starter_battery.charge < ignition_power_consumption)
			if(starter_battery.charge > ignition_power_consumption/5)
				playsound(loc, 'sound/effects/refill.ogg', 25, 1, 3) // flat sound
				current_start_chance = min((current_start_chance*0.2), can_start)
			else
				playsound(loc, 'sound/effects/refill.ogg', 25, 1, 3) // dead sound
				return FALSE
		else
			playsound(loc, 'sound/mecha/engine/engine_starting.ogg', 25, 1, 3) // normal start sound

		starter_battery.use(ignition_power_consumption)

	if(!prob(engine_initial_start_chance))
		return
	engine_start()
	return TRUE

/obj/item/mecha_parts/exosuit_engine/proc/handle_revs()
	if(!is_running)
		return
	if(revs == revs_selection)
		return
	revs = revs_selection

	if(revs < REVS_MID && engine_running_sound)
		engine_running_sound_high.stop()
		engine_running_sound.stop()
		engine_running_sound.start(skip_startsound = TRUE)
	if(revs >= REVS_MID && engine_running_sound)
		engine_running_sound.stop()
		engine_running_sound_high.stop()
		engine_running_sound_high.start(skip_startsound = TRUE)

/obj/item/mecha_parts/exosuit_engine/proc/engine_start()
	is_running = TRUE
	handle_revs()
	START_PROCESSING(SSobj, src)

/obj/item/mecha_parts/exosuit_engine/proc/engine_stop()
	is_running = FALSE
	engine_running_sound?.stop()
	engine_running_sound_high?.stop()
	revs = REVS_OFF
	if(body.chassis)
		if(body.chassis.power_status == IGNITION_ENGINE)
			body.chassis.power_status = IGNITION_AUX
			body.chassis.check_power(power_status = IGNITION_AUX)
	if(engine_power_pool)
		engine_power_pool.charge = 0
	STOP_PROCESSING(SSobj, src)

/obj/item/mecha_parts/exosuit_engine/attackby(obj/item/I, mob/user, params)
	if(is_electric)
		balloon_alert(user, "doesn't take fuel!")
		return
	if(istype(I, /obj/item/reagent_containers/jerrycan))
		var/obj/item/reagent_containers/jerrycan/gascan = I
		if(gascan.reagents.total_volume == 0)
			balloon_alert(user, "no fuel!")
			return
		if(fuel_amount >= fuel_max)
			balloon_alert(user, "full!")
			return
		var/fuel_transfer_amount = min(gascan.fuel_usage*2, gascan.reagents.total_volume)
		gascan.reagents.remove_reagent(/datum/reagent/fuel, fuel_transfer_amount)
		fuel_amount = min(fuel_amount + FUEL_PER_CAN_POUR, fuel_max)
		playsound(loc, 'sound/effects/refill.ogg', 25, 1, 3)
		balloon_alert(user, "[fuel_amount/fuel_max*100]%")
		return TRUE
	if(istype(I, /obj/item/tool/crowbar))
		balloon_alert(user, "removing battery")
		if(do_after(user, 5 SECONDS, src))
			starter_battery.forceMove(user)
			starter_battery = null
			return
	if(istype(I, /obj/item/cell) && !starter_battery)
		starter_battery.forceMove(src)
		starter_battery = I
	return ..()

/obj/item/mecha_parts/exosuit_engine/electric // currently it's just a gas engine in reverse. lol
	name = "electric exosuit engine"
	desc = "An electric power bank providing electrical energy to the exosuit's motors."
	engine_initial_start_chance = 100
	ignition_cycle_attempts = 1
	ignition_power_consumption = 0
	fuel_consumption = 0.1
	engine_power_generated = 30000
	is_electric = TRUE
	engine_running_sound = /datum/looping_sound/exosuit_engine_electric/sound_loop

/obj/item/mecha_parts/exosuit_engine/electric/add_battery(obj/item/cell/starting_battery/add_battery)
	QDEL_NULL(starter_battery)
	if(add_battery)
		add_battery.forceMove(src)
		starter_battery = add_battery
		return
	starter_battery = new /obj/item/cell/starting_battery/electric (src)

/obj/item/mecha_parts/exosuit_engine/electric/process()
	if(!is_functional || !is_running)
		return
	if(!starter_battery || starter_battery.charge <= 0)
		engine_stop()
		return
	if(engine_power_pool && !engine_power_pool.is_fully_charged())
		var/transfer_amount = min((engine_power_generated*GLOB.CELLRATE), starter_battery.charge)
		if(starter_battery.use(transfer_amount))
			engine_power_pool.give(transfer_amount)

/obj/item/mecha_parts/exosuit_engine/electric/attempt_engine_start()
	if(is_running)
		visible_message(span_warning("[src] is already running"))
		return FALSE
	if(!is_functional || !starter_battery || starter_battery.charge <= 0)
		return FALSE
	playsound(loc, engine_starting_sound, 25, 1, 3)
	engine_start()
	return TRUE

/obj/item/mecha_parts/exosuit_engine/proc/use_power(amount)
	if(!is_functional)
		return FALSE
	if(starter_battery && body.chassis.power_status == IGNITION_AUX)
		return starter_battery.use(amount)
	if(!engine_power_pool || engine_power_pool.charge < amount)
		return FALSE
	return engine_power_pool.use(amount)

/obj/item/mecha_parts/exosuit_engine/proc/has_power(amount)
	if(!is_functional || !engine_power_pool)
		return FALSE
	return engine_power_pool.charge

/obj/item/mecha_parts/exosuit_engine/proc/give_power(amount)
	if(!is_functional || !engine_power_pool)
		return FALSE
	return engine_power_pool.give(amount)

#warn write this down before i forget: make body components break first (disabled), and fall off second, if the chest falls off the mech dies
